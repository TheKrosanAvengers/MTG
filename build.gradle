//dependencies for pre build stuff
buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }

    dependencies {
        classpath("org.jooq:jooq-codegen:3.12.3")
        classpath("org.xerial:sqlite-jdbc:3.30.1")
        classpath('com.h2database:h2:1.4.200')
    }
}

//Plugins for gradle build
plugins {
    id "de.undercouch.download" version "4.0.4"
    id 'java'
}

//Repositories for java build dependencies
repositories {
    mavenLocal()
    mavenCentral()
}


//Dependencies for Java build
dependencies {
    compile("com.h2database:h2:1.4.200")
    compile("org.jooq:jooq:3.12.3")
    compile("org.jooq:jooq-codegen:3.12.3")
    compile("org.xerial:sqlite-jdbc:3.30.1")
    compile("org.apache.commons:commons-lang3:3.9")
    runtime 'com.h2database:h2:1.4.200'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.1'
}

//Each of the MTG Applications
task(startDB, dependsOn: 'classes', type: JavaExec) {
    main = 'org.krosanavengers.mtg.main'
    classpath = sourceSets.main.runtimeClasspath
}

//Settings for Java JUnit testing
test {
    useJUnitPlatform()
}

//=======   Generate MTGJSON JooQ objects   ===============
tasks.register("generateSQLITE") {
    doLast {
        def writer = new StringWriter()
        def xml = new groovy.xml.MarkupBuilder(writer)
                .configuration('xmlns': 'http://www.jooq.org/xsd/jooq-codegen-3.12.0.xsd') {
                    jdbc() {
                        driver('org.sqlite.JDBC')
                        url('jdbc:sqlite:AllPrintings.sqlite')
                        user('sa')
                        password('')
                    }
                    generator() {
                        database() {
                        }
                        generate([:]) {
                            pojos true
                            daos true
                        }
                        target() {
                            packageName('org.krosanavengers.mtg.jooq.mtgjson')
                            directory('src/main/java')
                        }
                    }
                }
        org.jooq.codegen.GenerationTool.generate(writer.toString())
    }
}


//=======   Create MTG JooQ objects   ===============
tasks.register("generateH2") {
    doLast {
        def writer = new StringWriter()
        def xml = new groovy.xml.MarkupBuilder(writer)
                .configuration('xmlns': 'http://www.jooq.org/xsd/jooq-codegen-3.12.0.xsd') {
                    jdbc() {
                        driver('org.h2.Driver')
                        url('jdbc:h2:file:./mtg')
                        user('sa')
                    }
                    generator() {
                        database() {
                        }
                        target() {
                            packageName('org.krosanavengers.mtg.jooq.h2')
                            directory('src/main/java')
                        }
                    }
                }
        org.jooq.codegen.GenerationTool.generate(writer.toString())
    }
}


//=======   Download Latest copy of MTGJSON sqlite db   ===============
task downloadFile(type: Download) {
    src 'https://www.mtgjson.com/files/AllPrintings.sqlite'
    dest projectDir
    overwrite true
}
//tasks.processResources.dependsOn.add(downloadFile)

